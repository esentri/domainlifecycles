buildscript {
    dependencies {
        classpath 'com.h2database:h2:2.3.232'
        classpath 'org.flywaydb:flyway-core:11.12.0'
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.springframework.boot' version '4.0.1'
    id 'org.flywaydb.flyway' version '11.12.0'
    id 'org.jooq.jooq-codegen-gradle' version '3.19.26'
    id 'io.domainlifecycles.dlc-gradle-plugin' version '3.0.0'
}


// ----------------------------------------------------------

def dbVersion = '2.4.240'
def dbDriver = 'org.h2.Driver'
def dbUrl = "jdbc:h2:file:${project.projectDir}/build/h2-db/test;NON_KEYWORDS=VALUE;AUTO_SERVER=TRUE"
def dbUsername = 'sa'
def dbPassword = ''
def dbMigration = 'src/main/resources/db/migration'

// ----------------------------------------------------------

bootJar {
    enabled = false
}

// ----------------------------------------------------------

tasks.register('runApp', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs '--add-opens java.base/java.lang=ALL-UNNAMED'
}

// ----------------------------------------------------------

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:4.0.1')


    implementation 'com.h2database:h2:2.4.240'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    implementation 'io.domainlifecycles:spring-boot-starter:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
    implementation 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    runtimeOnly 'org.jooq:jooq-codegen:3.19.26'
    jooqCodegen 'org.jooq:jooq-meta:3.19.26'

    testImplementation 'io.domainlifecycles:domain-diagrammer:3.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

// ----------------------------------------------------------

flyway {
    url = dbUrl
    user = dbUsername
    password = dbPassword
    driver = dbDriver
    defaultSchema = 'SHOP_DOMAIN_FLYWAY'
    createSchemas = true
    encoding = 'UTF-8'
    locations = ["filesystem:./${dbMigration}"]
}

// ----------------------------------------------------------

jooq {
    configuration {

        jdbc {
            driver = dbDriver
            url = dbUrl
            user = dbUsername
            password = dbPassword
        }

        generator {
            database {
                name = "org.jooq.meta.h2.H2Database"
                includes = ".*"
                inputSchema = 'SHOP_DOMAIN'
                recordVersionFields = 'CONCURRENCY_VERSION'
            }

            generate {}
            target {
                packageName = "sampleshop"
            }
        }
    }
}

tasks.named("compileJava") {
    dependsOn(tasks.named("jooqCodegen"))
}

tasks.named("jooqCodegen") {
    dependsOn(tasks.named("flywayMigrate"))
    inputs.files(fileTree("src/main/resources/db/migration"))
}

// ----------------------------------------------------------

dlcGradlePlugin {
    diagram {
        fileOutputDir = layout.buildDirectory
        diagrams {
            diagramNomnoml {
                domainModelPackages = ["sampleshop"]
                format = "nomnoml"
                fileName = "diagram"
            }
            diagramSvgComplete {
                domainModelPackages = ["sampleshop"]
                format = "svg"
                fileName = "complete"
                backgroundColor = "#ffffff"
            }
            diagramSvgAggregates {
                domainModelPackages = ["sampleshop"]
                format = "svg"
                fileName = "aggregates"
                backgroundColor = "#ffffff"
                showDomainEvents = false
                showDomainCommands = false
                showDomainServices = false
                showApplicationServices = false
                showRepositories = false
                showReadModels = false
                showQueryHandlers = false
                showOutboundServices = false

            }
        }
    }
}
// ----------------------------------------------------------

tasks.withType(Checkstyle).configureEach {
    enabled = false
}

test {
    finalizedBy jacocoTestReport
}

test {
    useJUnitPlatform()
}
jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
