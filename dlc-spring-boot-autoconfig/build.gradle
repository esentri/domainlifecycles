buildscript {
	dependencies {
		classpath libs.h2
		classpath libs.flyway.core
	}
}

plugins {
	id 'java-library'
    id 'maven-publish'
	id 'checkstyle'
    id 'jacoco'
	alias libs.plugins.spring.boot.plugin
	alias libs.plugins.flyway.plugin
	alias libs.plugins.jooq.codegen.gradle
	alias libs.plugins.yumi.gradle.licenser.plugin
	alias libs.plugins.jaredsburrows.license.plugin
}

def dbVersion = libs.versions.h2.version.get()
def dbDriver = 'org.h2.Driver'
def dbUrl = "jdbc:h2:file:${project.projectDir}/build/h2-db/test;NON_KEYWORDS=VALUE;AUTO_SERVER=TRUE"
def dbUsername = 'sa'
def dbPassword = ''
def dbMigration = 'src/test/resources/db/migration'

dependencies {
	api libs.jooq
    api libs.spring.jdbc
    api project(":domain-events-core")
    api project(":domain-events-jakarta-jta")
    api project(":domain-events-spring-tx")
    api project(":domain-events-gruelbox")
    api project(":jackson-integration")
    api project(":builder")
    api project(":jooq-integration")
    api project(":spring-doc-2-integration")
    api project(":spring-web-integration")
    api project(":swagger-v3-integration")
    api project(":domain-events-jakarta-jms")
    api project(":domain-events-activemq-classic-5")
    api project(":domain-events-mq")
    api project(":domain-events-serialization-jackson")
    api libs.gruelbox.transactionoutbox.spring
    api libs.spring.doc.openapi.starter.webmvc.ui
    api libs.spring.web
    api libs.jakarta.transaction.api
    api libs.jackson3.core
    api libs.jackson3.databind

    compileOnly libs.jooq.codegen
	jooqCodegen libs.jooq.meta

    testImplementation platform(libs.spring.boot.dependencies)
    testImplementation libs.h2
	testImplementation project(":test-shared-impl")
    testImplementation project(":types")
    testImplementation project(":builder")
    testImplementation project(":types")
    testImplementation project(":assertions")
    testImplementation project(":domain-events-core")
    testImplementation project(":domain-events-jakarta-jta")
    testImplementation project(":domain-events-spring-tx")
    testImplementation project(":domain-events-gruelbox")
    testImplementation project(":jackson-integration")
    testImplementation project(":jooq-integration")
    testImplementation project(":spring-doc-2-integration")
    testImplementation project(":spring-web-integration")
    testImplementation project(":swagger-v3-integration")
    testImplementation project(":bean-validations")
    testImplementation project(":domain-events-jakarta-jms")
    testImplementation project(":domain-events-mq")
    testImplementation project(":type-utils")
    testImplementation project(":domain-events-serialization-jackson")
	testImplementation libs.assertj
    testImplementation libs.gruelbox.transactionoutbox.core
    testImplementation libs.gruelbox.transactionoutbox.jackson
    testImplementation libs.gruelbox.transactionoutbox.spring
	testImplementation libs.spring.boot.starter.webmvc.test
    testImplementation libs.spring.boot.starter.jooq
    testImplementation libs.spring.boot.starter.jdbc
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.spring.boot.starter.artemis
    testImplementation libs.apache.active.mq.artemis.jms.server
    testImplementation libs.spring.doc.openapi.starter.webmvc.ui
    testImplementation libs.jackson3.core
    testImplementation libs.jackson3.databind
	testCompileOnly libs.lombok
	testAnnotationProcessor libs.lombok
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('sourcesJar', Jar) {
    exclude '**/build/generated/**', '**/generated/**', '**/source/kapt/**', '**/sources/annotationProcessor/**'
}

bootJar {
	enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''   // <â€” remove the "plain" classifier
}

// ----------------------------------------------------------

flyway {
	url = dbUrl
	user = dbUsername
	password = dbPassword
	driver = dbDriver
	defaultSchema = 'TEST_DOMAIN_FLYWAY'
	createSchemas = true
	encoding = 'UTF-8'
	locations = ["filesystem:./${dbMigration}"]
}

// ----------------------------------------------------------

jooq {
	configuration {

		jdbc {
			driver = dbDriver
			url = dbUrl
			user = dbUsername
			password = dbPassword
		}

		generator {
			database {
				name = "org.jooq.meta.h2.H2Database"
				includes = ".*"
				inputSchema = 'TEST_DOMAIN'
				recordVersionFields = 'CONCURRENCY_VERSION'
				forceIntegerTypesOnZeroScaleDecimals = true
			}

			generate {
				generatedAnnotation = false
				generatedAnnotationType = 'DETECT_FROM_JDK'
				javaTimeTypes = true
			}
			target {
				packageName = "io.domainlifecycles.test.autoconfig"
			}
		}
	}
}

tasks.named("compileJava") {
	dependsOn(tasks.named("jooqCodegen"))
}

tasks.named("jooqCodegen") {
	dependsOn(tasks.named("flywayMigrate"))
	inputs.files(fileTree("src/main/resources/db/migration"))
}

tasks.named("sourcesJar") {
    dependsOn(tasks.named("jooqCodegen"))
}

tasks.withType(Checkstyle) {
	enabled = false
}

test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
