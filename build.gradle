plugins {
    id 'org.jreleaser' version '1.13.1'
    id 'net.researchgate.release' version '3.0.2'
}

def versionProps = new Properties()
file("version.properties").withInputStream { versionProps.load(it) }

task printVersion {
    doFirst {
        println('Domain Lifecycles Version: ' + versionProps.getProperty("version"))
    }
}

task applyLicenses {
    subprojects { pr -> applyLicenses.dependsOn("${pr.path}:applyLicenses") }
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = false
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = ''
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'version.properties'
    versionProperties = ['version']
    snapshotSuffix = '-SNAPSHOT'
    scmAdapters = [
            net.researchgate.release.GitAdapter
    ]

    git {
        requireBranch.set('main')
        pushToRemote.set('origin')
        commitVersionFileOnly.set(true)
        signTag.set(false)
    }
}

ext {
    dlcGroup = 'io.domainlifecycles'
    dlcVersion = versionProps.getProperty("version")

    //lib versions
    slf4jVersion = '2.0.16'
    bytebuddyVersion = '1.15.10'
    classGraphVersion = '4.8.163'
    springDocOpenAPIVersion = '1.8.0'
    springDocOpenAPIVersion2 = '2.6.0'
    jacksonVersion = '2.17.2'
    activeMqClassicVersion = '5.18.4'
    javaxValidationApiVersion = '2.0.1.Final'
    jakartaValidationApiVersion = '3.0.2'
    jakartaTransactionApiVersion = '2.0.1'
    jakartaJmsApiVersion = '3.1.0'
    springTxVersion = '6.1.13'
    springJdbcVersion = '6.1.13'
    swaggerVersion = '2.2.25'
    springWebVersion = '5.3.33'
    springWeb6Version = '6.1.13'
    jooqVersion = '3.19.6'
    commonsCollectionsVersion = '3.2.2'
    typeResolverVersion = '1.0.3'
    gruelboxVersion = '5.5.447'
    commonsValidatorVersion = '1.7'

    //test lib versions
    logbackClassicVersion = '1.5.3'
    hibernateValidatorJakartaVersion = '8.0.1.Final'
    glassfishJakartaVersion = '4.0.2'
    jakartaElVersion = '5.0.1'
    springBoot3PlatformVersion = '3.3.4'
    springBoot2PlatformVersion = '2.7.18'
    kryoVersion = '5.3.0'
    h2Version = '2.2.224'
    assertJVersion = '3.24.2'
    activeMqArtemisVersion = '2.37.0'
    lombokVersion = '1.18.30'
    jaxBApiVersion = '2.3.0'
    jaxBCoreVersion = '4.0.4'
    jaxBImplVersion = '4.0.4'
    atomikosJtaVersion = '6.0.0'
    junitJupiterParamsVersion = '5.10.0'
    mockitoCoreVersion = '5.5.0'
    awaitilityVersion = '4.2.0'
    javaxJmsApiVersion = '2.0.1'
    commonsIOVersion = '2.14.0'

    //test javax based bean validation (even though lib names involve jakarta ;-) )
    hibernateValidatorJavaxVersion = '6.2.3.Final'
    glassfishJakartaElVersion = '3.0.2'
    jakartaElApiVersion = '3.0.2'

    //plugin versions
    quiltmcLicenserVersion = '2.0.1'
    nuStuderJooqVersion = '7.1.1'
    releasePluginVersion = '3.0.2'
    flywayVersion = '9.22.1'
    gradleLicenserVersion = '1.1.0'
    jaredsburrowsLicenseVersion = '0.9.8'
}

allprojects {

    plugins.withType(JavaPlugin) {
        repositories {
            mavenCentral()
        }
        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
        }
        test {
            useJUnitPlatform()
            testLogging {
                for (def level : LogLevel.values()) {
                    def testLogging = get(level)
                    testLogging.exceptionFormat = 'full'
                    testLogging.events = ["FAILED", "STANDARD_OUT", "STANDARD_ERROR"]
                }
            }
            maxParallelForks(1)
        }
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }
        tasks.withType(Jar) {
            includeEmptyDirs = false
            enabled = true
        }
        tasks.withType(JavaCompile) {
            options.encoding = "UTF-8"
        }
        javadoc {
            title = '<h1>DomainLifecycles</h1>'
            options.bottom = '<i>provided by esentri.</i>'
            options.encoding = 'UTF-8'
            options.docEncoding = 'UTF-8'
            options.charSet = 'UTF-8'
            //options.links("https://docs.oracle.com/en/java/javase/${buildJavaVersion}/docs/api/")
            options.addBooleanOption 'html5', true
            options.tags(
                    'apiNote:a:API Note:',
                    'implSpec:a:Implementation Requirements:',
                    'implNote:a:Implementation Note:'
            )

        }
    }

    plugins.withId('checkstyle') {
        checkstyle {
            toolVersion = '10.12.4'
            configFile = rootProject.file('./checkstyle/checkstyle.xml')
            ignoreFailures = true
            showViolations = true

            checkstyleMain.source = "src/main/java"
        }
    }

    plugins.withId('dev.yumi.gradle.licenser') {
        checkLicenseMain.enabled = true
        checkLicenseTest.enabled = false
        license {
            // Add a license header rule, at least one must be present.
            rule(file("./copyright/HEADER"))
            // Apply license header ONLY to Java files
            exclude("**/tests/**/*.java")
            exclude("**/test/**/*.java")
            exclude("**/sampleshop/**/*.java")
        }
    }

    plugins.withId('maven-publish') {

        afterEvaluate {
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        groupId = dlcGroup
                        version = dlcVersion

                        from components.java

                        pom {
                            name = 'Domain Lifecycles'
                            description = 'A Java library for building DDD applications'
                            url = 'https://www.esentri.com'

                            licenses {
                                license {
                                    name = 'The Apache License, Version 2.0'
                                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                }
                            }
                            developers {
                                developer {
                                    id = 'mherb'
                                    name = 'Mario Herb'
                                    email = 'mario.herb@esentri.com'
                                    organization = 'esentri AG'
                                    organizationUrl = 'https://www.esentri.com'
                                }
                                developer {
                                    id = 'dgaller'
                                    name = 'Dominik Galler'
                                    email = 'dominik.galler@esentri.com'
                                    organization = 'esentri AG'
                                    organizationUrl = 'https://www.esentri.com'
                                }
                            }
                            scm {
                                connection = 'scm:git@github.com:esentri/domainlifecycles.git'
                                url = 'https://github.com/esentri/domainlifecycles/'
                            }
                        }
                    }
                }

                repositories {
                    maven {
                        url = '../build/staging-deploy'
                    }
                }
            }
        }
    }
}

tasks.release.dependsOn('applyLicenses')

// ----------------------------------------------------------
//  RELEASE TO MAVEN CENTRAL
// ----------------------------------------------------------

jreleaser {
    project {
        java.groupId = 'io'
        version = versionProps.getProperty("version")
        description = 'A Java library for building DDD applications'
        copyright = 'The Apache License, Version 2.0'
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

// ----------------------------------------------------------
//  GRADLE.WRAPPER
// ----------------------------------------------------------

wrapper {
    gradleVersion = '8.10.1'
    distributionType = 'ALL'
}

